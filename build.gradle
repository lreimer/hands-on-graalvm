plugins {
    id 'java'
    id 'application'
    id 'groovy'
}

repositories {
    jcenter()
}

sourceCompatibility = "1.8"
targetCompatibility = "1.8"

dependencies {
    implementation 'info.picocli:picocli:4.5.2'
    implementation 'info.picocli:picocli-jansi-graalvm:1.2.0'

    runtime 'info.picocli:picocli-codegen:4.1.4'
    runtime 'info.picocli:picocli-shell-jline3:4.1.4'

    // Use the latest Groovy version for Spock testing
    testImplementation 'org.codehaus.groovy:groovy-all:2.5.4'

    // Use the awesome Spock testing and specification framework even with Java
    testImplementation 'org.spockframework:spock-core:1.2-groovy-2.5'
    testImplementation 'junit:junit:4.12'
}

// Define the main class for the application
mainClassName = 'hands.on.graalvm.HandsOnGraal'

task copyRuntimeLibs(type: Copy) {
    into "$buildDir/libs"
    from sourceSets.main.runtimeClasspath
    include "*.jar"
}

assemble.finalizedBy copyRuntimeLibs

task reflectionConfigGenerator(description: 'Generate reflection config', dependsOn: 'assemble', type: JavaExec) {
    main = 'picocli.codegen.aot.graalvm.ReflectionConfigGenerator'
    classpath = sourceSets.main.runtimeClasspath
    args = ['hands.on.graalvm.HandsOnGraal', '--output', 'build/reflect.json']
}

String getClasspath() {
    'libs/' + file("$buildDir/libs").list()?.join(':libs/')
}

task graalNativeImage(description: 'Generate native image with GraalVM', dependsOn: 'reflectionConfigGenerator', type: Exec) {
    workingDir "$buildDir"
    commandLine = [
            'native-image',
            '-cp', getClasspath(),
            '-H:ReflectionConfigurationFiles=reflect.json',
            '-H:+ReportUnsupportedElementsAtRuntime',
            '-H:+ReportExceptionStackTraces',
            '-H:+AddAllCharsets',
            '-H:+TraceClassInitialization',
            '--initialize-at-run-time=org.fusesource.jansi.WindowsAnsiOutputStream',
            '--no-server',
            '--enable-http',
            '--enable-https',
            'hands.on.graalvm.HandsOnGraal',
            'hands-on-graal'
    ]
}

task polyglotMayhem(description: 'Run embedded polyglot language example', dependsOn: 'testClasses', type: JavaExec) {
    main = 'hands.on.graalvm.PolyglotMayhem'
    classpath = sourceSets.test.runtimeClasspath
}
